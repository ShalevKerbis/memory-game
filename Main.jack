// Main.jack
/**
 * Memory Game - Main controller
 * Flow: opening -> mode menu -> game loop -> replay
 */
class Main {

    /** Waits for replay decision (R = replay, Q = quit) */
    function boolean waitForReplay() {
        var char k;

        do Output.moveCursor(16, 15);
        do UI.printL("Press R to play again or Q to quit");

        let k = 0;
        while (~((k = 82) | (k = 114) | (k = 81) | (k = 113))) {
            let k = Keyboard.keyPressed();
        }
        while (~(Keyboard.keyPressed() = 0)) { }

        if ((k = 82) | (k = 114)) {
            return true;
        }
        return false;
    }

    function void main() {

        var Board board;
        var Computer cpu;

        var int playerScore;
        var int computerScore;

        var int seed;
        var int mode;              // 1 = vs computer, 2 = 2 players
        var int diff;              // 1, 2, 3
        var int size;              // 8, 12, 16
        var boolean firstRun;
        var boolean playAgain;
        var boolean finished;
        var boolean isPlayerTurn;

        var int i1, i2;
        var int v1, v2;
        var boolean success;

        let firstRun = true;
        let playAgain = true;

        while (playAgain) {

            let playerScore = 0;
            let computerScore = 0;

            if (firstRun) {
                do GameScreen.runOpeningSequence();
                let firstRun = false;
            }

            // -------- MODE SELECTION --------
            do Screen.clearScreen();
            do GameScreen.drawFrame();
            do GameScreen.drawModeMenu();

            let seed = 0;
            let mode = 0;

            while (~((mode = 49) | (mode = 50))) {   // '1' or '2'
                let seed = seed + 1;
                let mode = Keyboard.keyPressed();
            }
            while (~(Keyboard.keyPressed() = 0)) { }

            if (mode = 49) { let mode = 1; }
            else { let mode = 2; }

            // -------- DIFFICULTY SELECTION --------
            do Screen.clearScreen();
            do GameScreen.drawFrame();
            
            do Output.moveCursor(10, 20);
            do UI.printL("Select Difficulty:");
            do Output.moveCursor(12, 20);
            do UI.printL("1. Easy (6 cards)");
            do Output.moveCursor(14, 20);
            do UI.printL("2. Medium (10 cards)");
            do Output.moveCursor(16, 20);
            do UI.printL("3. Hard (14 cards)");

            let diff = 0;
            while (~((diff = 49) | (diff = 50) | (diff = 51))) { // '1','2','3'
                let seed = seed + 1;
                let diff = Keyboard.keyPressed();
            }
            while (~(Keyboard.keyPressed() = 0)) { }
            let size = (diff - 48) * 4 + 2; // '1'(49)->6, '2'(50)->10, '3'(51)->14

            // -------- INIT GAME --------
            let board = Board.new(seed, size);
            let cpu = Computer.new(board, seed);
            let isPlayerTurn = true;

            do Screen.clearScreen();
            do GameScreen.drawFrame();
            do UI.drawHeader(mode);
            do UI.showScore(playerScore, computerScore);
            do board.draw();

            let finished = false;

            // -------- MAIN GAME LOOP --------
            while (~finished) {

                if (isPlayerTurn) {
                    // ===== PLAYER 1 TURN =====
                    do UI.showTurn(true, mode);

                    let i1 = UI.readCardIndex(board, 1);
                    do board.reveal(i1);
                    do board.draw();

                    let i2 = UI.readCardIndexDifferent(board, 2, i1);
                    do board.reveal(i2);
                    do board.draw();

                    let v1 = board.getValue(i1);
                    let v2 = board.getValue(i2);

                    if (v1 = v2) {
                        do board.setMatched(i1);
                        do board.setMatched(i2);
                        let playerScore = playerScore + 1;
                        do UI.printCenterMessage("Match! +1 point");
                        do Sys.wait(1500);
                        do UI.clearStatus();
                        // Player keeps turn
                    } else {
                        do UI.printCenterMessage("No match. Press key to flip over...");
                        do GameScreen.waitForKey();
                        do UI.clearStatus();
                        do board.hideIfNotMatched(i1);
                        do board.hideIfNotMatched(i2);
                        let isPlayerTurn = false;
                    }
                } else {
                    // ===== OPPONENT TURN =====
                    if (mode = 1) {
                        // --- Computer ---
                        do UI.showTurn(false, mode);

                        let success = cpu.playTurn();
                        if (success) {
                            let computerScore = computerScore + 1;
                            do UI.printCenterMessage("Computer matched! +1");
                            do Sys.wait(1500);
                            do UI.clearStatus();
                            // Computer keeps turn
                        } else {
                            do UI.printCenterMessage("Computer missed.");
                            do Sys.wait(1500);
                            do UI.clearStatus();
                            let isPlayerTurn = true;
                        }
                    } else {
                        // --- Player 2 ---
                        do UI.showTurn(false, mode);

                        let i1 = UI.readCardIndex(board, 1);
                        do board.reveal(i1);
                        do board.draw();

                        let i2 = UI.readCardIndexDifferent(board, 2, i1);
                        do board.reveal(i2);
                        do board.draw();

                        let v1 = board.getValue(i1);
                        let v2 = board.getValue(i2);

                        if (v1 = v2) {
                            do board.setMatched(i1);
                            do board.setMatched(i2);
                            let computerScore = computerScore + 1;
                            do UI.printCenterMessage("Player 2 matched! +1");
                            do Sys.wait(1500);
                            do UI.clearStatus();
                            // Player 2 keeps turn
                        } else {
                           do UI.printCenterMessage("No match. Press key to flip over...");
                           do GameScreen.waitForKey();
                           do UI.clearStatus();
                           do board.hideIfNotMatched(i1);
                           do board.hideIfNotMatched(i2);
                           let isPlayerTurn = true;
                        }
                    }
                }

                do UI.showScore(playerScore, computerScore);
                do board.draw();
                let finished = board.finished();
            }

            // -------- GAME OVER --------
            do Screen.clearScreen();
            do board.dispose();
            do cpu.dispose();
            do UI.printWinner(playerScore, computerScore, mode);
            let playAgain = Main.waitForReplay();
        }

        do Screen.clearScreen();
        return;
    }
}
